Function GetCompIP(sNetMask As String) As String
'Возвращает IP-adreess компа по аргументу подсети
'Например: Debug.Print GetCompIP("192.168.1")
 
Dim strComputer$, i%
Dim objWMIService As Object
Dim IPConfigSet As Object
Dim IPConfig As Object
 
    strComputer = "."
    Set objWMIService = GetObject("winmgmts:" _
        & "{impersonationLevel=impersonate}!\\" & strComputer & "\root\cimv2")
 
    Set IPConfigSet = objWMIService.ExecQuery _
        ("Select * from Win32_NetworkAdapterConfiguration Where IPEnabled=TRUE")
 
    For Each IPConfig In IPConfigSet
        If Not IsNull(IPConfig.IPAddress) Then
            For i = LBound(IPConfig.IPAddress) To UBound(IPConfig.IPAddress)
                If InStr(1, IPConfig.IPAddress(i), sNetMask) Then
                    GetCompIP = Trim(IPConfig.IPAddress(i))
                End If
            Next
        End If
    Next
End Function